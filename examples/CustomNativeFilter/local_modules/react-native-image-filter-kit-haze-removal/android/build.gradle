buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.library'

def _ext = rootProject.ext;

def _reactNativeVersion = _ext.has('reactNative') ? _ext.reactNative : '+';
def _compileSdkVersion = _ext.has('compileSdkVersion') ? _ext.compileSdkVersion : 29;
def _buildToolsVersion = _ext.has('buildToolsVersion') ? _ext.buildToolsVersion : '29.0.2';
def _minSdkVersion = _ext.has('minSdkVersion') && _ext.minSdkVersion > 16 ? _ext.minSdkVersion : 17;
def _targetSdkVersion = _ext.has('targetSdkVersion') ? _ext.targetSdkVersion : 29;

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName "1.0"
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compileOnly "com.facebook.react:react-native:${_reactNativeVersion}"
    implementation project(':react-native-image-filter-kit')
    implementation fileTree(dir: "libs", include: ["*.jar"])
}
